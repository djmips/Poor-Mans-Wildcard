 ORG $8000
 OBJ $8000

 LST OFF

NMIL = $FFFA
NMIH = $FFFB

VTAB = $FC22
HOME = $FC58
INIT = $FB2F
ROMB = $C600


* ZPAG  EQUATES

ORGNL = $00
ORGNH = $01
DESTL = $02
DESTH = $03

LMOD = $1A
* LMOD IS THE LENGTH OF
* THE MODIFICATION TO THE
* F8 ROM (CASSETTE WRITE
* ROUTINE AT $FECD REPLACED)
*

* IMPORTS

INTER = $FECD

CLOC = $D800
DISPVAL = CLOC+7
START = CLOC+8

ROMP = $8200

* EXPORTS:(IF THESE CHANGE
* THEN THE OTHER PROGS WILL HAVE
* TO BE RE-ASSEMBLED)
*
* BOOT1
* RESUME


* RWTS PARMS

PARM = $DFB8
VOLUME = PARM+$03
TRACK = PARM+$04
SECTOR = PARM+$05
DCTP = PARM+$06
BUFF = PARM+$08
BYTECNT = PARM+$0B
COMND = PARM+$0C
ERRCODE = PARM+$0D


 JSR INIT
 JSR HOME
 JSR LPRINT
 ASC "POOR MAN'S WILDCARD "
 INV "V2.5"
 HEX 8D

 HEX 8D
 ASC "COPYRIGHT 1984-85"
 HEX 8D
 ASC "BY SHOE STRING ENTERPRISES"
 HEX 8D
 HEX 8D
 HEX 00

 BIT $C093
 BIT $C093
 LDA #$00
 STA $D000
 LDA $D000
 BNE ERROR
 LDA #$FF
 STA $D000
 LDA $D000
 CMP #$FF
 BEQ PRESNT
ERROR JSR LPRINT

 ASC "16 RAM CARD NOT IN SLOT ONE!"
 HEX 8D
 ASC "TURN OFF COMPUTER BEFORE MOVING!"
 HEX 8D8D
 HEX 00
 BIT $C090
 RTS

PRESNT BIT $C091
 BIT $C091

 LDA #$00
 STA ORGNL
 STA DESTL

 LDA #$D0
 STA ORGNH
 STA DESTH

 LDX #$30
 JSR MOVLAN

 BIT $C09B
 BIT $C09B


 LDA #ROMP
 STA ORGNL
 LDA #/ROMP
 STA ORGNH

 LDA #CLOC
 STA DESTL
 LDA #/CLOC
 STA DESTH
 LDX #$08
 JSR MOVLAN

 LDX #LMOD
STINT LDA MONMOD,X
 STA INTER,X
 DEX
 BPL STINT

 LDX #$17
TPARM LDA $B7E8,X
 STA PARM,X
 DEX
 BPL TPARM
 LDA #$00
 STA VOLUME
 STA BYTECNT

 LDA #$CB
 STA DCTP
 LDA #$DF
 STA DCTP+1

 LDA #<INTER+1
 STA NMIL
 LDA #>INTER+1
 STA NMIH

 LDA #$01
 STA DISPVAL

 BIT $C090

 JSR LPRINT
 ASC "POOR MAN'S WILDCARD "
 ASC "IS INSTALLED"
 HEX 8D
 ASC "AND READY TO USE !"
 HEX 8D8D
 HEX 00


 BIT $C090
 RTS

 LDX #$10
MOVLAN LDY #$00
MVL LDA (ORGNL),Y
 STA (DESTL),Y
 INY
 BNE MVL
 INC ORGNH
 INC DESTH
 DEX
 BNE MVL
 RTS

CHOUT EQU $FDF0

ZL = $FE
ZH = $FF

LPRINT LDA #$00
 STA ZL
 PLA
 TAY
 PLA
 STA ZH
 JMP PRINT2

LOOP LDA (ZL),Y
 BEQ DONE
 JSR CHOUT
PRINT2 INY
 BNE LOOP
 INC ZH
 BNE LOOP
DONE LDA ZH
 PHA
 TYA
 PHA
 RTS


MONMOD RTI
 BIT $C09B
 BIT $C09B
 DEC NMIL
 JMP START

BOOT1 LDA #/ROMB
 PHA
 LDA #ROMB
 PHA
 PHA

RESUME INC NMIL
 BIT $C090
EMOD RTI

QR = EMOD-MONMOD

 LST ON
