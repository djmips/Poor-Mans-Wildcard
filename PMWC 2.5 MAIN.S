
 LST OFF

*IMPORTS

RWTS = $DE00
RESUME = $FEE1
BOOT1 = $FEDA


VTAB = $FC22
VTABZ = VTAB+$02
HOME = $FC58
SETNORM = $FE84
INIT = $FB2F
SETVID = $FE93
SETKBD = $FE89
BELL = $FBDD
KBD = $C000
KBDSTRB = $C010

RETURN = $8D

* ZPAG  EQUATES

ORGNL = $00
ORGNH = $01
DESTL = $02
DESTH = $03
CH = $24
CV = $25
TBAS = $28
SELNUM = $F9
HORZ = $FA


CLOC = $D800

PARM = $DFB8
VOLUME = PARM+$03
TRACK = PARM+$04
SECTOR = PARM+$05
DCTP = PARM+$06
BUFF = PARM+$08
BYTECNT = PARM+$0B
COMND = PARM+$0C
ERRCODE = PARM+$0D



 ORG CLOC
 OBJ $8200

ONE HEX 00
TWO HEX 00
THREE HEX 00
ASAV HEX 00
YSAV HEX 00
XSAV HEX 00
SSAV HEX 00
DISPVAL HEX 00

START STA ASAV
 STY YSAV
 STX XSAV
 PLA
 STA ONE
 PLA
 STA TWO
 PLA
 STA THREE
 TSX
 STX SSAV

 LDX #$00
LDF2 LDA $00,X
 STA $D000,X
 LDA $100,X
 STA $D100,X
 INX
 BNE LDF2
 JSR MOVE

 LDX #$08
 STX SELNUM
BEGIN JSR SETNORM
 JSR INIT
 JSR SETVID
 JSR SETKBD

 JSR HOME
 JSR TITLE
 JSR FLIPLINE

KEYIN BIT KBDSTRB
KEY LDA KBD
 BPL KEY
 CMP #RETURN
 BEQ SELECT
 PHA
 JSR FLIPLINE
 PLA
 CMP #$88
 BNE NOTUP
 DEC SELNUM
 LDA SELNUM
 CMP #08
 BCS NOCHG0
 LDA #13
 STA SELNUM
 BNE NOCHG0
NOTUP INC SELNUM
 LDA SELNUM
 CMP #14
 BCC NOCHG0
 LDA #08
 STA SELNUM
NOCHG0 JSR FLIPLINE
 JMP KEYIN

SELECT BIT $C010
 LDX SELNUM
 LDA JTABH-8,X
 PHA
 LDA JTABL-8,X
 PHA
 RTS

JTABL DFB #SSCRN-1
 DFB #CONTIN-1
 DFB #BOOT-1
 DFB #SAVED-1
 DFB #LOADD-1
 DFB #MONIT-1
 DFB #SSCRN-1

JTABH DFB #/SSCRN-1
 DFB #/CONTIN-1
 DFB #/BOOT-1
 DFB #/SAVED-1
 DFB #/LOADD-1
 DFB #/MONIT-1
 DFB #/SSCRN-1

SSCRN JSR TEXTEX
SS LDA DISPVAL
 CMP #$0B
 BCC GOSET
 LDA #$00
 STA DISPVAL
GOSET JSR SETDISP
 BIT $C010
WAITKEY LDA $C000
 BPL WAITKEY
 CMP #RETURN
 BEQ BACK
 INC DISPVAL
 BNE SS
BACK JSR TEXTEX
 JMP BEGIN

CONTIN JSR SETDISP
 JSR MOVE
 LDX #$00
STF2 LDA $D000,X
 STA $00,X
 LDA $D100,X
 STA $100,X
 INX
 BNE STF2

 LDX SSAV
 TXS
 LDA THREE
 PHA
 LDA TWO
 PHA
 LDA ONE
 PHA
 LDY YSAV
 LDX XSAV
 LDA ASAV
 JMP RESUME

BOOT JSR HOME
 JMP BOOT1

MONIT LDA #$4C
 STA $3F8
 LDA #BEGIN
 STA $3F9
 LDA #/BEGIN
 STA $3FA
 JMP $FF59

SAVED LDA #$02
 JSR RORW
 JMP BEGIN

LOADD LDA #$01
 JSR RORW
 JMP BEGIN

RORW STA COMND
 LDA #$03
 STA TRACK
 LDA #$00
 STA BUFF
 LDA #$08
 STA BUFF+1
RWLOOP LDA #$0F
 STA SECTOR
RWLOOP1 LDY #PARM
 LDA #/PARM
 JSR RWTS
 BCS DSKERR
INCAG INC BUFF+1
 LDA BUFF+1
 CMP #$D9
 BEQ DNRW
 AND #$F0
 CMP #$C0
 BEQ INCAG
 DEC SECTOR
 BPL RWLOOP1
 INC TRACK
 BNE RWLOOP
DNRW RTS

DSKERR JSR BELL
 JSR BELL
 RTS

TEXTEX LDX #$D4
 STX ORGNH
 LDX #$04
 STX DESTH
 BNE CMOV
MOVE LDA #$02
 STA ORGNH
 LDA #$D2
 STA DESTH
 LDX #$06
CMOV LDY #$00
 STY ORGNL
 STY DESTL
TEXCH LDA (ORGNL),Y
 PHA
 LDA (DESTL),Y
 STA (ORGNL),Y
 PLA
 STA (DESTL),Y
 INY
 BNE TEXCH
 INC DESTH
 INC ORGNH
 DEX
 BNE TEXCH
 RTS


TITLE LDA #10
 STA HORZ
 LDX #00
 JSR PRINT
 LDA #14
 STA HORZ
 LDX #SELECTS-TEXT
 JSR PRINT
 RTS

PRINT LDY #$00
PRT1 LDA HORZ
 STA CH
 JSR VTAB
PRT2 LDA TEXT,X
 BEQ DPRT
 JSR $FDF0
 INX
 CMP #$8D
 BNE PRT2
 BEQ PRT1
DPRT JMP $FD8E


FLIPLINE LDA SELNUM
 JSR VTABZ
 LDY #39
FLIP LDA (TBAS),Y
 CMP #$A0
 BEQ NTSP
 EOR #$C0
 STA (TBAS),Y
NTSP DEY
 BPL FLIP
 RTS


SETDISP LDY #$06
 LDA DISPVAL
SETLOOP LSR
 PHA
 TYA
 ADC #$00
 TAX
 LDA $C050,X
 PLA
 DEY
 DEY
 BPL SETLOOP
 RTS

TEXT INV ":                   :"
 HEX 8D
 INV " "
 ASC  "POOR MAN'S WILDCARD"
 INV " "
 HEX 8D
 INV " "
 ASC  "BY DAVID GALLOWAY  "
 INV " "
 HEX 8D
 INV " "
 ASC  " (C) 1984/85 BY    "
 INV " "
 HEX 8D
 INV " "
 ASC  "SHOE-STRING ENT.   "
 INV " "
 HEX 8D
 INV ":                   :"
 HEX 8D8D
 HEX 00

SELECTS ASC "SET"
 HEX E0
 ASC "SCREEN"
 HEX 8D
 ASC "RESUME"
 HEX 8D
 ASC "BOOT"
 HEX E0
 ASC "DISK"
 HEX 8D
 ASC "SAVE"
 HEX E0
 ASC "IMAGE"
 HEX 8D
 ASC "LOAD"
 HEX E0
 ASC "IMAGE"
 HEX 8D
 ASC "ENTER"
 HEX E0
 ASC "MONITOR"
 HEX 8D
 HEX 00
ERRMSG ASC "DISK ERROR #"
 HEX 00

 LST ON
